#!/usr/bin/env ./node_modules/coffee-script/bin/coffee

cluster = require 'cluster'
swarm   = require './src'

swarm.configure
	namespace: 'test'

CONSUMERS = parseInt process.argv[2]
TRIALS    = parseInt process.argv[3]

sum = (items) ->
	result = 0
	result += item for item in items
	return result

random = (max) ->
	Math.floor(Math.random() * max) + 1

produce = ->
	producer = swarm.createProducer()
	timings  = []
	reportFindings = ->
		average = sum(timings) / timings.length
		console.log "The average time taken was #{average} ms"
		process.exit(0)
	for trial in [1..TRIALS]
		do (trial) ->
			numbers = (random(11) for [1..random(11)])
			started = Date.now()
			producer.queue 'math', 'add', numbers, (err, result) ->
				elapsed = Date.now() - started
				timings.push elapsed
				reportFindings() if trial == TRIALS

consume = ->
	consumer = swarm.createConsumer('math')
	consumer.on 'add', (items, callback) ->
		callback null, sum(items)

if cluster.isMaster
	console.log "Running #{TRIALS} trials with #{CONSUMERS} consumers"
	cluster.fork() for i in [1..CONSUMERS]
	produce()
else
	consume()
